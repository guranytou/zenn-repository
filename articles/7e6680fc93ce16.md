---
title: "AWS&Terraformã§Webã‚µãƒ¼ãƒã¨ALBã‚’ç«‹ã¦ã¦ã¿ã‚‹"
emoji: "ğŸŒ±"
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["AWS", "Terraform"]
published: true
---

# æƒ³å®šèª­è€…
- Terraformã®æ§‹æ–‡ã¯å‹‰å¼·ã—ãŸã‘ã©ã€ãƒªã‚½ãƒ¼ã‚¹ä½œæˆã¯ã“ã‚Œã‹ã‚‰ã®æ–¹
    - æ§‹æˆä¾‹ã‚’å…ƒã«æ‰‹ã‚’å‹•ã‹ã—ã¦ã¿ãŸã„æ–¹
- AWSã‚’è»½ãè§¦ã£ãŸã“ã¨ãŒã‚ã‚‹æ–¹

# ã¯ã˜ã‚ã«
Terraformåˆå­¦è€…ã®äººãŒAWSä¸Šã§å®Ÿéš›ã«æ‰‹ã‚’å‹•ã‹ã—ã¦ä½œã‚Œã‚‹æ§‹æˆã‚’è€ƒãˆã¦ã¿ã¾ã—ãŸã€‚  
æ§‹æˆå›³ã¯ä»¥ä¸‹ã®é€šã‚Šã§ã™ã€‚  
![EC2-ALB](https://user-images.githubusercontent.com/42028429/163096473-ff8f0407-a8b5-45a3-9e80-e92844154ae2.png)

å…¨ã¦ã®ã‚³ãƒ¼ãƒ‰ã¯ä»¥ä¸‹ã®ãƒªãƒã‚¸ãƒˆãƒªã«æ ¼ç´ã•ã‚Œã¦ã„ã¾ã™ã€‚  
https://github.com/guranytou/tf-ec2-alb

å„ãƒªã‚½ãƒ¼ã‚¹ã®ãƒ‘ãƒ©ãƒ¡ãƒ¼ã‚¿ã«ã¤ã„ã¦ã¯å…¬å¼ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã‚’ã”ç¢ºèªãã ã•ã„ã€‚  
https://registry.terraform.io/providers/hashicorp/aws/latest

# äº‹å‰æº–å‚™
terraformã‚’åˆ©ç”¨ã™ã‚‹ãŸã‚ã«ã€`main.tf`ã‚’ç”¨æ„ã—ã¾ã™ã€‚  
ã¾ãŸåˆ©ç”¨ã—ãŸã„AWSã®regionã¯ã“ã“ã§è¨­å®šã—ã¾ã™ï¼ˆä»Šå›ã¯`ap-northeasw-1`ï¼ˆæ±äº¬ï¼‰ã‚’æŒ‡å®šï¼‰
```hcl: main.tf
terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.9"
    }
  }
}

variable "aws_access_key" {}
variable "aws_secret_access_key" {}

provider "aws" {
  region     = "ap-northeast-1"
  access_key = var.aws_access_key
  secret_key = var.aws_secret_access_key
}
```

AWSã®Credentialã‚’è¨­å®šã—ã¦ãŠãå¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚  
ä»Šå›ã¯ã‚µã‚¯ãƒƒã¨ä½¿ã„ãŸã„ã®ã§ã€`terraform.tfvars`ã‚’åˆ©ç”¨ã™ã‚‹ã“ã¨ã¨ã—ã¾ã™ã€‚  

```hcl: terraform.tfvars
aws_access_key        = "YOUR_AWS_ACCESS_KEY"
aws_secret_access_key = "YOUR_AWS_SECRET_ACCESS_KEY"
```

ä¸Šè¨˜ã‚’ç”¨æ„å¾Œã€CLIä¸Šã§`terraform init`ã‚’å©ãã€`Terraform has been successfully initialized!`ã¨è¡¨ç¤ºã•ã‚Œã‚‹ã“ã¨ã‚’ç¢ºèªã—ã¾ã—ã‚‡ã†ã€‚

# è§£èª¬
1. VPCã‚’ä½œæˆã™ã‚‹
ã¾ãšã¯VPCã‚’ä½œæˆã—ã¦ã„ãã¾ã™ã€‚CIDRãªã©ã¯ãŠå¥½ããªã‚‚ã®ã§å¤§ä¸ˆå¤«ã§ã™ã€‚  
Nameã‚¿ã‚°ã¯è‡ªåˆ†ãŒä»Šå›ä½œã£ãŸãƒªã‚½ãƒ¼ã‚¹ã ã¨ã‚ã‹ã‚‹ã‚ˆã†ãªã‚‚ã®ã«å¤‰æ›´ã—ã¦ãã ã•ã„ã€‚  
ï¼ˆãã®ä»–ã®ãƒªã‚½ãƒ¼ã‚¹ã‚‚åŒæ§˜ã§ã™ãŒã€ã“ã“ã§ã¯ä¾‹ã®ãŸã‚exampleã¨ã—ã¦ã„ã¾ã™ï¼‰
```hcl: network.tf
resource "aws_vpc" "example" {
  cidr_block = "10.100.0.0/16"

  tags = {
    Name = "example"
  }
}
```

è©¦ã—ã«ã“ã“ã§ `terraform plan`ã‚’å©ãã€ä»¥ä¸‹ã®è¡¨ç¤ºãŒå‡ºã‚‹ã“ã¨ã‚’ç¢ºèªã—ã¾ã—ã‚‡ã†ã€‚
```bash
  # aws_vpc.example will be created
  + resource "aws_vpc" "example" {
      + arn                                  = (known after apply)
      + cidr_block                           = "10.100.0.0/16"
      + default_network_acl_id               = (known after apply)
      + default_route_table_id               = (known after apply)
      + default_security_group_id            = (known after apply)
      + dhcp_options_id                      = (known after apply)
      + enable_classiclink                   = (known after apply)
      + enable_classiclink_dns_support       = (known after apply)
      + enable_dns_hostnames                 = (known after apply)
      + enable_dns_support                   = true
      + id                                   = (known after apply)
      + instance_tenancy                     = "default"
      + ipv6_association_id                  = (known after apply)
      + ipv6_cidr_block                      = (known after apply)
      + ipv6_cidr_block_network_border_group = (known after apply)
      + main_route_table_id                  = (known after apply)
      + owner_id                             = (known after apply)
      + tags                                 = {
          + "Name" = "example"
        }
      + tags_all                             = {
          + "Name" = "example"
        }
    }

Plan: 1 to add, 0 to change, 0 to destroy.
```

ã§ã¯å®Ÿéš›ã«`terraform apply`ã‚’å®Ÿè¡Œã—ã€ãƒªã‚½ãƒ¼ã‚¹ã‚’ä½œæˆã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚
`Enter a value`ã¨èã‹ã‚Œã‚‹ã®ã§ã€`yes`ã‚’å…¥åŠ›ã—ã¾ã™ã€‚  
ä»¥ä¸‹ã®è¡¨ç¤ºãŒå‡ºã‚Œã°ä½œæˆã§ãã¦ã„ã‚‹ã®ã§ã€GUIä¸Šã§ã‚‚ç¢ºèªã—ã¦ã¿ã¾ã—ã‚‡ã†ã€‚
```bash
aws_vpc.example: Creating...
aws_vpc.example: Creation complete after 1s [id=vpc-02c546f38de3782ac]

Apply complete! Resources: 1 added, 0 changed, 0 destroyed.
```

ã“ã‚Œä»¥é™ã®ã‚¹ãƒ†ãƒƒãƒ—ã§ã‚‚éƒ½åº¦`terraform plan`åŠã³`terraform apply`ã‚’è¡Œã£ã¦ã€å®Ÿéš›ã«ãƒªã‚½ãƒ¼ã‚¹ãŒã§ãã¦ã„ã‚‹ã‹ç¢ºèªã—ã¦ã„ãã¾ã—ã‚‡ã†ã€‚

2. ã‚µãƒ–ãƒãƒƒãƒˆã‚’ä½œæˆã™ã‚‹
æ¬¡ã«ã‚µãƒ–ãƒãƒƒãƒˆã‚’ä½œæˆã—ã¦ã„ãã¾ã™ã€‚CIDRãªã©ã¯ãŠå¥½ããªã‚‚ã®ã§å¤§ä¸ˆå¤«ã§ã™ã€‚  
ä»Šå›public subnetã‚’2ã¤ä½œæˆã—ã¦ã„ã¾ã™ãŒã€ã“ã‚Œã¯ALBã«ã‚µãƒ–ãƒãƒƒãƒˆã‚’ç™»éŒ²ã™ã‚‹éš›ã«2ã¤ä»¥ä¸Šã®AZã«ã¾ãŸãŒã£ã¦ã„ã‚‹å¿…è¦ãŒã‚ã‚‹ãŸã‚ã§ã™ã€‚  
å®Ÿéš›ã«ä»Šå›åˆ©ç”¨ã™ã‚‹ã®ã¯1aã®ã¿ã§ã™ï¼ˆãã®ãŸã‚privateã¯1aã®ã¿ä½œæˆã—ã¦ã„ã¾ã™ï¼‰
```hcl: network.tf
# public
resource "aws_subnet" "example_public_1a" {
  vpc_id                  = aws_vpc.example.id
  cidr_block              = "10.100.0.0/24"
  availability_zone       = "ap-northeast-1a"

  tags = {
    Name = "example_public_1a"
  }
}

resource "aws_subnet" "example_public_1c" {
  vpc_id                  = aws_vpc.example.id
  cidr_block              = "10.100.1.0/24"
  availability_zone       = "ap-northeast-1c"

  tags = {
    Name = "example_public_1c"
  }
}

# private
resource "aws_subnet" "example_private_1a" {
  vpc_id                  = aws_vpc.example.id
  cidr_block              = "10.100.100.0/24"
  availability_zone       = "ap-northeast-1a"

  tags = {
    Name = "example_private_1a"
  }
}

```

3. IGW/NAT GWã‚’ä½œæˆã™ã‚‹
ã‚¤ãƒ³ã‚¿ãƒ¼ãƒãƒƒãƒˆã‹ã‚‰ã¨ã®æ¥ç¶šã«å¿…è¦ãªInternet Gatewayã¨NAT Gatewayã‚’ä½œæˆã—ã¾ã™ã€‚
```hcl: network.tf
# Internet GW
resource "aws_internet_gateway" "example" {
  vpc_id = aws_vpc.example.id

  tags = {
    Name = "example"
  }
}

# NAT GW
resource "aws_nat_gateway" "example" {
  allocation_id = aws_eip.nat_gw.id
  subnet_id     = aws_subnet.example_public_1a.id
  depends_on    = [aws_internet_gateway.example]

  tags = {
    Name = "example"
  }
}

# EIP(NATGW)
resource "aws_eip" "nat_gw" {
  vpc = true
}
```

4. IGW/NATGWç”¨ã®ãƒ«ãƒ¼ãƒˆãƒ†ãƒ¼ãƒ–ãƒ«ã‚’ä½œæˆã™ã‚‹
ãƒ«ãƒ¼ãƒˆãƒ†ãƒ¼ãƒ–ãƒ«ã«ã¦ã€å„ã‚µãƒ–ãƒãƒƒãƒˆã¨Internet GatewayåŠã³NAT Gatewayã¨ã®çµŒè·¯ã‚’ç”¨æ„ã—ã¾ã™ã€‚
```hcl: network.tf
# RouteTable/Public
resource "aws_route_table" "example_internet_gw" {
  vpc_id = aws_vpc.example.id

  tags = {
    Name = "example_route_internet_gw"
  }
}

resource "aws_route" "example_internet_gw_default" {
  route_table_id         = aws_route_table.example_internet_gw.id
  destination_cidr_block = "0.0.0.0/0"
  gateway_id             = aws_internet_gateway.example.id
}

resource "aws_route_table_association" "example_internet_gw_1a" {
  subnet_id      = aws_subnet.example_public_1a.id
  route_table_id = aws_route_table.example_internet_gw.id
}

resource "aws_route_table_association" "example_internet_gw_1c" {
  subnet_id      = aws_subnet.example_public_1c.id
  route_table_id = aws_route_table.example_internet_gw.id
}

# RouteTable/Private
resource "aws_route_table" "example_private" {
  vpc_id = aws_vpc.example.id

  tags = {
    Name = "example_route_private"
  }
}

resource "aws_route" "example_private_default" {
  route_table_id         = aws_route_table.example_private.id
  destination_cidr_block = "0.0.0.0/0"
  nat_gateway_id         = aws_nat_gateway.example.id
}

resource "aws_route_table_association" "example_private_1" {
  subnet_id      = aws_subnet.example_private_1a.id
  route_table_id = aws_route_table.example_private.id
}
```

5. EC2ã‚’ä½œæˆã™ã‚‹
Private Subnetã«EC2ã‚’ä½œæˆã—ã¾ã™ã€‚åˆã‚ã›ã¦EC2ã«ä»˜ä¸ã™ã‚‹SGã‚‚ä½œæˆã—ã¦ã„ãã¾ã™ã€‚
```hcl: server.tf
# EC2
resource "aws_instance" "example" {
  ami                    = "ami-0992fc94ca0f1415a"
  instance_type          = "t2.micro"
  vpc_security_group_ids = [aws_security_group.example_for_instance.id]
  subnet_id              = aws_subnet.example_private_1a.id

  user_data = file("install_apache.sh")

  tags = {
    Name = "example"
  }
}

# SG(ALB Allow)
resource "aws_security_group" "example_for_instance" {
  name   = "example_instance"
  vpc_id = aws_vpc.example.id

  tags = {
    Name = "example_for_instance"
  }
}

resource "aws_security_group_rule" "sg_ingress_instance" {
  type                     = "ingress"
  from_port                = 80
  to_port                  = 80
  protocol                 = "tcp"
  source_security_group_id = aws_security_group.example_for_alb.id
  security_group_id        = aws_security_group.example_for_instance.id
}

resource "aws_security_group_rule" "sg_egress_instance" {
  type              = "egress"
  from_port         = 0
  to_port           = 0
  protocol          = "-1"
  cidr_blocks       = ["0.0.0.0/0"]
  security_group_id = aws_security_group.example_for_instance.id
}
```

ä»Šå›ã¯user_dataã‚‚ä½œæˆã¨åˆã‚ã›ã¦èª­ã¿è¾¼ã¿ãŸã„ã®ã§ã€user_dataç”¨ã«`install_apache.sh`ã‚‚ä½œæˆã—ã¾ã™ã€‚  
```bash: install_apache.sh
#! /bin/bash

yum update -y
yum install -y httpd
systemctl start httpd
systemctl enable httpd
```

6. ALBã‚’ä½œæˆã™ã‚‹
EC2ã®å‰é¢ã«ALBã‚’é…ç½®ã—ãŸã„ã®ã§ã€Public Subnetã«ALBã‚’ä½œæˆã—ã¾ã™ã€‚  
åˆã‚ã›ã¦HTTPã®ã¿è¨±å¯ã™ã‚‹SGã‚’ä½œæˆã—ã¾ã™ã€‚
```hcl: server.tf
# ALB
resource "aws_lb" "example" {
  name                             = "example"
  load_balancer_type               = "application"
  internal                         = false
  security_groups                  = [aws_security_group.example_for_alb.id]
  subnets = [
    aws_subnet.example_public_1a.id,
    aws_subnet.example_public_1c.id
  ]

  tags = {
    Name = "example"
  }
}

# SG
resource "aws_security_group" "example_for_alb" {
  name   = "example_http"
  vpc_id = aws_vpc.example.id

  tags = {
    Name = "example_for_alb"
  }
}

resource "aws_security_group_rule" "sg_ingress" {
  type              = "ingress"
  from_port         = 80
  to_port           = 80
  protocol          = "tcp"
  cidr_blocks       = ["0.0.0.0/0"]
  security_group_id = aws_security_group.example_for_alb.id
}

resource "aws_security_group_rule" "sg_egress" {
  type              = "egress"
  from_port         = 0
  to_port           = 0
  protocol          = "-1"
  cidr_blocks       = ["0.0.0.0/0"]
  security_group_id = aws_security_group.example_for_alb.id
}
```

7. ALBç”¨ã®Listener/TGã‚’ä½œæˆã™ã‚‹ 
å…ˆã»ã©ä½œæˆã—ãŸEC2ã‚’ALBã¨ç´ä»˜ã‘ã—ãŸã„ã®ã§ã€Listenerã¨Target Groupã‚’ä½œæˆã—ã¾ã™ã€‚  
ã¾ãŸã€ä»Šå›Apacheã®ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆç”»é¢ã‚’è¡¨ç¤ºã•ã›ã‚‹é–¢ä¿‚ä¸Šã€health_checkã®matcherã‚’403ã«ã—ã¦ã„ã¾ã™ã€‚  
ï¼ˆApacheã®ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆç”»é¢ã®ã‚¹ãƒ†ãƒ¼ã‚¿ã‚¹ã‚³ãƒ¼ãƒ‰ãŒ403ã§ã‚ã‚‹ãŸã‚ï¼‰
```hcl: service.tf
# LB - Listener
resource "aws_lb_listener" "example" {
  load_balancer_arn = aws_lb.example.arn
  port              = 80
  protocol          = "HTTP"
  default_action {
    type             = "forward"
    target_group_arn = aws_lb_target_group.example.arn
  }
}

# LB - TargetGroup
resource "aws_lb_target_group" "example" {
  name     = "example"
  port     = 80
  protocol = "HTTP"
  vpc_id   = aws_vpc.example.id

  health_check {
    path                = "/"
    matcher             = 403
    port                = 80
    protocol            = "HTTP"
  }

  depends_on = [aws_lb.example]
}

resource "aws_lb_target_group_attachment" "example" {
  target_group_arn = aws_lb_target_group.example.arn
  target_id        = aws_instance.example.id
  port             = 80
}
```

8. ç¢ºèªã™ã‚‹
`terraform apply`ã‚’è¡Œã„ã€å„ãƒªã‚½ãƒ¼ã‚¹ã‚’applyã—çµ‚ãˆãŸã‚‰ã€ALBã®DNSåã‹ã‚‰ã‚¢ã‚¯ã‚»ã‚¹ã—ã¦ã¿ã¾ã™ã€‚  
Apacheã®ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆç”»é¢ãŒè¡¨ç¤ºã•ã‚Œã‚Œã°OKã§ã™ã€‚

# ã¾ã¨ã‚
AWS&Terraformã§ALB-EC2ã®æ§‹æˆã‚’ç°¡å˜ã«ä½œã£ã¦ã¿ã¾ã—ãŸã€‚  
ã“ã®è¨˜äº‹ã‚’å‚è€ƒã«ãƒªã‚½ãƒ¼ã‚¹ã‚’ä½œã£ã¦ã¿ã¦ã€å°‘ã—ã§ã‚‚Terraform&AWSã®ç†è§£ã®åŠ©ã‘ã«ãªã‚Œã°å¹¸ã„ã§ã™ã€‚
